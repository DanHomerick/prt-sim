<?xml version="1.0" encoding="utf-8"?>
<mx:TitleWindow xmlns:mx="http://www.adobe.com/2006/mxml" layout="absolute" width="600" height="300"
				title="Select Vehicle..."
	            showCloseButton="true"
	            initialize="initData()"
	            close="PopUpManager.removePopUp(this);" >
	            
	<mx:Script>
		<![CDATA[
			import mx.events.ListEvent;
			import mx.collections.SortField;
			import mx.collections.Sort;
			import mx.collections.ArrayCollection;
			import mx.managers.PopUpManager;
			import edu.ucsc.track_builder.Globals;
			
			[Bindable]
			public var vehiclesArray:ArrayCollection;
			
			public function initData():void {
				vehiclesArray = new ArrayCollection();
				for each (var overlay:VehicleOverlay in Globals.vehicles.overlays) {
					var vehicle:Vehicle = overlay.vehicle;
					vehiclesArray.addItem({id:vehicle.id,
					                       location:vehicle.location.label ? vehicle.location.label : vehicle.location.id,
					                       position:Utility.truncateTo(vehicle.position, 2),
					                       velocity:vehicle.velocity,
					                       model:vehicle.model,
					                       vehicle:vehicle,
					                       overlay:overlay});
				}
				var sort:Sort = new Sort();
				sort.fields = [new SortField("id")]
				vehiclesArray.sort = sort
				vehiclesArray.refresh();
			}
			
			public function onClick(event:ListEvent):void {
				try {
					var overlay:VehicleOverlay = vehiclesArray[event.rowIndex].overlay;
					var vehicle:Vehicle = vehiclesArray[event.rowIndex].vehicle;
					// Change the location and zoom to center on the piece;
					Globals.map.setCenter(vehicle.latlng);
					var zoom:Number = 18;
					Globals.map.setZoom(zoom);
					overlay.onSelect(null);
				} catch (e:TypeError) {
					trace(e);	
				}
			}
			
		]]>
	</mx:Script>

	<mx:DataGrid width="100%" height="100%" dataProvider="{this.vehiclesArray}" itemClick="this.onClick(event);">
        <mx:columns>
            <mx:DataGridColumn dataField="id" headerText="ID" width="100"/>  
            <mx:DataGridColumn dataField="location" headerText="Location" width="200"/>
			<mx:DataGridColumn dataField="position" headerText="Position" width="100"/>
			<mx:DataGridColumn dataField="velocity" headerText="Velocity" width="100"/>
			<mx:DataGridColumn dataField="model" headerText="Model" width="100"/>			
        </mx:columns>
	</mx:DataGrid>
	
</mx:TitleWindow>